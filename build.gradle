buildscript {
    repositories {
        jcenter()
		maven {
			url = 'https://maven.minecraftforge.net/'
		}
		maven {
			url = 'https://repo.spongepowered.org/maven'
		}
    }
    dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '6.0.+', changing: true
		classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'org.spongepowered.mixin'


version = "${minecraft_version}-${mod_version}"
group= "${package_group}" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "${mod_name}"

sourceCompatibility = targetCompatibility = "1.8"

minecraft {
	mappings channel: mappings_channel, version: mappings_version
	accessTransformer = file("src/main/resources/spartanweaponry_at.cfg")
}

repositories {
	mavenCentral()
	maven {
		url = 'https://maven.minecraftforge.net/'
	}
	maven {
		url = 'https://repo.spongepowered.org/maven'
	}
	maven {
		name = "CurseMaven"
		url = "https://cursemaven.com/"
	}
	flatDir {
		dirs 'libs'
	}
}

configurations {
	embed
		compile.extendsFrom(embed)
}

dependencies {
	minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

	annotationProcessor 'org.spongepowered:mixin:0.8.3-SNAPSHOT:processor'

	implementation "org.spongepowered:mixin:${mixin_version}"
	implementation fg.deobf("curse.maven:baubles-227083:2518667")
	implementation fg.deobf("curse.maven:trinkets-and-baubles-279900:3604719")
	implementation fg.deobf("curse.maven:potioncore-242872:2905184")
	implementation fg.deobf("curse.maven:quality-tools-264756:2732994")
	implementation fg.deobf("curse.maven:reach-fix-556777:4344825")
	implementation fg.deobf("curse.maven:rlcombat-711714:4341524")
	implementation fg.deobf("curse.maven:spartan-hud-baubles-557117:3566426")
	implementation fg.deobf("curse.maven:somanyenchantments-267035:3574478")
	implementation fg.deobf("curse.maven:jei-238222:2578467")
	implementation fg.deobf("curse.maven:mixinbootstrap-357178:3437402")
}

sourceSets {
	main {
		ext.refMap = "spartanweaponry.mixin.refmap.json"
	}
}

jar {
	from(configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {
		exclude "LICENSE.txt", "META-INF/MANIFEST.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
	}
	manifest.attributes(
		"FMLCorePluginContainsFMLMod": "true",
		"ForceLoadAsMod": "true",
		"FMLCorePlugin": "com.oblivioussp.spartanweaponry.mixin.MixinLoader",
		"FMLAT": "spartanweaponry_at.cfg",
		"TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
		"TweakOrder": 0,
		"MixinConfigs": "spartanweaponry.mixins.json"
    )
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", minecraft_version

	filesMatching('mcmod.info') {
		expand 'mod_id': mod_id, 'mod_name': mod_name, 'version': mod_version,
				'mcversion': minecraft_version
	}

	rename '(.+_at.cfg)', 'META-INF/$1'
}
